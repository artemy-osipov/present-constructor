import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
    id 'org.jetbrains.kotlin.kapt' version '1.7.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.21'
    id 'com.bnorm.power.kotlin-power-assert' version '0.12.0'
    id 'org.springframework.boot' version '3.0.0'
    id 'org.graalvm.buildtools.native' version '0.9.18'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'io.github.artemy.osipov.shop'
version = '1.5.0-SNAPSHOT'

dependencies {
    kapt enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)
    implementation enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)

    kapt 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'io.sentry:sentry-spring-boot-starter-jakarta:6.8.0'
    implementation 'io.sentry:sentry-logback:6.8.0'

    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.postgresql:r2dbc-postgresql'

    implementation 'com.github.f4b6a3:ulid-creator:5.1.0'
    implementation 'fr.opensagres.xdocreport:fr.opensagres.xdocreport.document.docx:2.0.4'
    implementation 'fr.opensagres.xdocreport:fr.opensagres.xdocreport.template.freemarker:2.0.4'

    implementation 'io.github.microutils:kotlin-logging-jvm:3.0.4'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'

    implementation 'io.projectreactor:reactor-tools'

    def mapstructVersion = '1.5.3.Final'
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    kapt "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.docx4j:docx4j-JAXB-ReferenceImpl:11.4.8'
    testImplementation 'org.testcontainers:cockroachdb:1.17.6'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.6.RELEASE'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-debug'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    environment.putAll([
            "BP_NATIVE_IMAGE_BUILD_ARGUMENTS": "--initialize-at-build-time=ch.qos.logback,org.apache.logging --initialize-at-run-time=io.netty"
    ])
}

graalvmNative {
    metadataRepository {
        enabled = true
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
}

tasks.withType(Test).configureEach {
    jvmArgs += ["-XX:+AllowRedefinitionToAddDeleteMethods"]
}
