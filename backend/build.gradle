plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.kapt' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
    id 'com.bnorm.power.kotlin-power-assert' version '0.11.0'
    id 'org.springframework.boot' version '2.6.7'
    id 'org.springframework.experimental.aot' version '0.11.4'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'io.github.artemy.osipov.shop'
version = '1.5.0-SNAPSHOT'

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release' }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.jetbrains.kotlinx') {
            details.useVersion '1.6.1'
        }
    }
}

dependencies {
    kapt platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

    kapt 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'io.r2dbc:r2dbc-postgresql'

    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'
    implementation 'fr.opensagres.xdocreport:fr.opensagres.xdocreport.document.docx:2.0.3'
    implementation 'fr.opensagres.xdocreport:fr.opensagres.xdocreport.template.freemarker:2.0.3'

    runtimeOnly 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'io.github.microutils:kotlin-logging-jvm:2.1.21'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    implementation 'io.projectreactor:reactor-tools'

    def mapstructVersion = '1.4.2.Final'
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    kapt "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.docx4j:docx4j-JAXB-ReferenceImpl:11.3.2'
    testImplementation 'org.testcontainers:cockroachdb:1.17.1'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.6.RELEASE'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-debug'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
}
